
configurations {
    customMbGenerator
}

dependencies {
    customMbGenerator 'org.mybatis.generator:mybatis-generator-core:1.4.2'
    customMbGenerator 'org.postgresql:postgresql:42.7.7'
    // 事前にコンパイルするプラグインをクラスパスに含める
    customMbGenerator sourceSets.main.output
}

task mbGenerator {
    doFirst {
        def datasource = getDatasource()
        ant.properties['url'] = datasource.url
        ant.properties['userId'] = datasource.username
        ant.properties['password'] = datasource.password
        ant.properties['driverClass'] = datasource.driverClassName
        ant.properties['targetProject'] = "${projectDir}/src/main/java".toString()
        ant.taskdef(name: 'mbgenerator', classname: 'org.mybatis.generator.ant.GeneratorAntTask', classpath: configurations.customMbGenerator.asPath)
        ant.mbgenerator(overwrite: true, configfile: "${resourcesPath}/mybatis/generatorConfig.xml", verbose: true) {
            propertyset {
                propertyref(name: 'url')
                propertyref(name: 'userId')
                propertyref(name: 'password')
                propertyref(name: 'driverClass')
                propertyref(name: 'targetProject')
            }
        }
    }
}

mbGenerator.group = 'mybatis'
// プラグインを事前にコンパイルさせる
mbGenerator.dependsOn compileJava
